buildscript {
    repositories {
        mavenLocal()
        maven { url "http://ves-ebi-de:8081/artifactory/gradle" }
        mavenCentral()
    }
    dependencies {
        classpath("org.sonarqube.gradle:gradle-sonarqube-plugin:1.1")
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
    }
}

def version_num = '2.1.2'

version = version_num
ext.archivesBaseName = 'ena-taxonomy-client'

apply plugin: 'io.codearte.nexus-staging'
apply plugin: "jacoco"
apply plugin: 'signing'
apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'

group = 'uk.ac.ebi.ena.taxonomy'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

ext { springVersion = "4.0.5.RELEASE" }

test {
    filter { includeTestsMatching "*Should" }
}

dependencies {
    compile "org.springframework:spring-web:${springVersion}"
    compile "com.fasterxml.jackson.core:jackson-core:2.3.3"
    compile "com.fasterxml.jackson.core:jackson-databind:2.3.3"
    testCompile "junit:junit:4.11"
    testCompile "com.google.code.bean-matchers:bean-matchers:0.9"
    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile "com.github.tomakehurst:wiremock:1.42"
}

jar {
    manifest {
        attributes(
                "Implementation-Title": archivesBaseName,
                'Implementation-Version': version,
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
        )
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

artifacts {
    archives javadocJar
    archives jar
    archives sourceJar
}

signing {
    sign configurations.archives
}


task wrapper(type: Wrapper) { gradleVersion = '2.9' }

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name archivesBaseName
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'ENA Taxonomy Service'
                url 'ena taxonomy service'

                scm {
                    url 'https://github.com/enasequence/ena-taxonomy-client'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'neilg'
                        name 'Daniel Vaughan'
                        email 'dvaughan@ebi.ac.uk'
                    }
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = "uk.ac.ebi.ena.taxonomy"
}


